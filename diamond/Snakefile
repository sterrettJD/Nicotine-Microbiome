# run with 
# snakemake --cluster "sbatch" --cluster-config cluster_config.yml -j 20 --latency-wait 60

project_ids = ["PRJNA548383", "PRJNA544061", "PRJNA508385"]
acc_ids = []

for project in project_ids:
    with open("../Accession_lists/"+project+"_Acc_List.txt", "r") as f:
        acc_ids += [line.strip() for line in f.readlines()]

rule all: #for fiji
    input:
        # merge_reads creates these
        expand("../data/{sampleid}/{sampleid}.assembled.fastq",
        sampleid=acc_ids),
        expand("../data/{sampleid}/{sampleid}.discarded.fastq",
        sampleid=acc_ids),
        expand("../data/{sampleid}/{sampleid}.unassembled.forward.fastq",
        sampleid=acc_ids),
        expand("../data/{sampleid}/{sampleid}.unassembled.reverse.fastq",
        sampleid=acc_ids),

        # trim_short_reads creates these
        expand("../data/{sampleid}/{sampleid}.assembled.75.fastq",
        sampleid=acc_ids),

        # diamond creates a results file
        expand("../data/{sampleid}/diamond_results_{sampleid}.txt",
        sampleid=acc_ids),

        # uniprot mapping creates this
        expand("../data/{sampleid}/diamond_results_{sampleid}_mapped.txt",
        sampleid=acc_ids),

        # concat creates this
        "../gene_hits.tsv",

        # diamond_single_copy_marker_genes creates these
        expand("../data/{sampleid}/marker_diamond_results_{sampleid}.txt",
        sampleid=acc_ids),

        # concat_single_copy_hits creates this
        "../marker_gene_hits.tsv"

"""
rule get_data:
    output:
        "../data/{sampleid}/{sampleid}_1.fastq",
        "../data/{sampleid}/{sampleid}_2.fastq",
        "../data/{sampleid}/{sampleid}/{sampleid}.sra",
    run:
        commands = [
        "prefetch {wildcards.sampleid}", 
        "fasterq-dump {wildcards.sampleid} --outdir ../data/{wildcards.sampleid} --progress"
        ]

        for c in commands:
            shell(c)
"""
# merge reads using PEAR
rule merge_reads:
    threads: 20
    input:
        "../data/{sampleid}/{sampleid}_1.fastq",
        "../data/{sampleid}/{sampleid}_2.fastq"
    output:
        "../data/{sampleid}/{sampleid}.assembled.fastq",
        "../data/{sampleid}/{sampleid}.discarded.fastq",
        "../data/{sampleid}/{sampleid}.unassembled.forward.fastq",
        "../data/{sampleid}/{sampleid}.unassembled.reverse.fastq"
    shell:
        """ 
        pear -f ../data/{wildcards.sampleid}/{wildcards.sampleid}_1.fastq \
        -r ../data/{wildcards.sampleid}/{wildcards.sampleid}_2.fastq \
        -o ../data/{wildcards.sampleid}/{wildcards.sampleid} \
        --threads 20
        """

# trim merged reads using custom script
rule trim_short_reads:
    input:
        "../data/{sampleid}/{sampleid}.assembled.fastq"
    output:
        "../data/{sampleid}/{sampleid}.assembled.75.fastq"
    shell: 
        """
        cutadapt --minimum-length 75 \
        -o ../data/{wildcards.sampleid}/{wildcards.sampleid}.assembled.75.fastq \
        ../data/{wildcards.sampleid}/{wildcards.sampleid}.assembled.fastq 
        """

# run diamond on trimmed merged reads
rule run_diamond:
    threads: 20
    input:
        "../data/{sampleid}/{sampleid}.assembled.75.fastq"
    output:
        "../data/{sampleid}/diamond_results_{sampleid}.txt"
    shell:
        """
        diamond blastx --db ../database-building/uniprot-nicotine-db-fiji.dmnd \
        -q ../data/{wildcards.sampleid}/{wildcards.sampleid}.assembled.75.fastq \
        --threads 20 > ../data/{wildcards.sampleid}/diamond_results_{wildcards.sampleid}.txt
        """ 

# map diamond hits to uniprot gene name
rule map_uniprot:
    input:
        "../data/{sampleid}/diamond_results_{sampleid}.txt"
    output:
        "../data/{sampleid}/diamond_results_{sampleid}_mapped.txt"
    shell:
        """
        python map_uniprot_hits.py \
        -i ../data/{wildcards.sampleid}/diamond_results_{wildcards.sampleid}.txt \
        -o ../data/{wildcards.sampleid}/diamond_results_{wildcards.sampleid}_mapped.txt
        """

# concatenate all diamond outputs to one file
rule concat_diamond_mapped_outputs:
    input:
        expand("../data/{sampleid}/diamond_results_{sampleid}_mapped.txt",
        sampleid=acc_ids)
    output:
        "../gene_hits.tsv"
    shell:
        """
        cat {input} >> ../gene_hits.tsv
        """

# run diamond for single copy marker genes on trimmed merged reads
rule run_diamond_single_copy_marker_genes:
    threads: 20
    input:
        "../data/{sampleid}/{sampleid}.assembled.75.fastq"
    output:
        "../data/{sampleid}/marker_diamond_results_{sampleid}.txt"
    shell:
        """
        diamond blastx --db ../database-building/marker_gene_db.dmnd \
        -q ../data/{wildcards.sampleid}/{wildcards.sampleid}.assembled.75.fastq \
        --threads 20 > ../data/{wildcards.sampleid}/marker_diamond_results_{wildcards.sampleid}.txt
        """

# concatenate all diamond outputs to one file
rule concat_single_copy_hits:
    input:
        expand("../data/{sampleid}/marker_diamond_results_{sampleid}.txt",
        sampleid=acc_ids)
    output:
        "../marker_gene_hits.tsv"
    shell:
        """
        cat {input} >> ../marker_gene_hits.tsv
        """
